version = '1.1'

allprojects {
    apply plugin: 'idea'
    idea.module.outputDir file("out/production/classes")
}

subprojects {
    apply plugin: 'java'
    group = 'nl.edulogo'
    sourceCompatibility = 1.10
    repositories {
        mavenCentral()
    }
    afterEvaluate {
        compileJava {
            if (project.hasProperty("moduleName")) {
                inputs.property("moduleName", moduleName)
                doFirst {
                    options.compilerArgs = [
                            '--module-path', classpath.asPath,
                    ]
                    classpath = files()
                }
            }
        }

        compileTestJava {
            if (project.hasProperty("moduleName")) {
                inputs.property("moduleName", moduleName)
                doFirst {
                    options.compilerArgs = [
                            '--module-path', classpath.asPath,
                            '--add-modules', 'junit',
                            '--add-reads', "$moduleName=junit",
                            '--patch-module', "$moduleName=" + files(sourceSets.test.java.srcDirs).asPath,
                    ]
                    classpath = files()
                }
            }
        }

        test {
            if (project.hasProperty("moduleName")) {
                inputs.property("moduleName", moduleName)
                doFirst {
                    jvmArgs = [
                            '--module-path', classpath.asPath,
                            '--add-modules', 'ALL-MODULE-PATH',
                            '--add-reads', "$moduleName=junit",
                            '--patch-module', "$moduleName=" + files(sourceSets.test.java.outputDir).asPath,
                    ]
                    classpath = files()
                }
            }
        }
    }

    //Default dependencies for all
    dependencies {
        testCompile group: 'junit', name: 'junit', version: '4.12'
    }

    apply plugin: "maven-publish"
    publishing {
        repositories {
            maven {
                name = "GitHubPackages"
                url = uri("https://maven.pkg.github.com/UnderKoen/EduLogo")
                credentials {
                    username = "UnderKoen"
                    password = "token"
                }
            }
        }
        publications {
            gpr(MavenPublication) {
                from(components.java)
            }
        }
    }
}

apply plugin: 'java'
group = 'nl.edulogo'
sourceCompatibility = 1.10

project(':Core') {
    version = '1.1'
    ext.moduleName = 'edulogo.core'

    dependencies {
    }
}

project(':Display') {
    version = '1.1'
    ext.moduleName = 'edulogo.display'

    dependencies {
        compile project(':Core')
    }
}

project(':JavaLogo') {
    version = '1.1'
    ext.moduleName = 'edulogo.javalogo'

    dependencies {
        compile project(':Core')
        compile project(':Display')
        compile project(':Logo')
    }
}

project(':Editor') {
    version = '1.1'
    ext.moduleName = 'edulogo.editor'

    dependencies {
        compile project(':Core')
        compile project(':Display')
    }
}

project(':ACSLogo') {
    version = '1.1'
    ext.moduleName = 'edulogo.acslogo'

    dependencies {
        compile project(':Core')
        compile project(':Display')
        compile project(':Editor')
        compile project(':Logo')
    }
}

project(':Logo') {
    version = '1.1'
    ext.moduleName = 'edulogo.logo'

    dependencies {
        compile project(':Core')
    }
}